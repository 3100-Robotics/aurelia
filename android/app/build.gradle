plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    id 'com.github.node-gradle.node'
}



android {
    namespace 'com.lt3100.aurelia'
    compileSdk 35

    defaultConfig {
        applicationId "com.lt3100.aurelia"
        minSdk 28
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
    implementation 'androidx.webkit:webkit:1.9.0'
}



/**
 * Node plugin configuration
 * https://github.com/node-gradle/gradle-node-plugin/blob/master/docs/usage.md#configuring-the-plugin
 */
node {
    // The Node.js project directory location
    // This is where the package.json file and node_modules directory are located
    nodeProjectDir.set(file("../../"))
    npmInstall.args = ['--legacy-peer-deps']
}

/**
 * This task builds the vite project at ../../web folder.
 */
def buildWeb = tasks.register('buildWeb', NpmTask) {
    description = 'Runs vite build'
    args = ['run', 'build-android']
    doFirst {
        println 'Executing `npm run build` in ../../web'
    }
    dependsOn npmInstall
    dependsOn cleanAssets
    dependsOn cleanWeb
}

/**
 * Removes the assets generated by the `buildWeb` task.
 */
def cleanWeb = tasks.register('cleanWeb', Delete) {
    description = 'Removes web assets generated by vite'
    delete '../../dist'
}

/**
 * This task cleans the Android assets folder.
 * This is necessary because the Android build process does not clean the assets folder.
 */
def cleanAssets = tasks.register('cleanAssets', Delete) {
    description = 'Removes web assets from app module'
    delete 'src/main/assets/dist'
}

/**
 * This task copies the the vite project at `../../web/dist` folder to the Android assets folder.
 */
def copyDistToAssets = tasks.register('copyDistToAssets', Copy) {
    description = 'Copies web assets into app module'
    from '../../dist'
    into 'src/main/assets/dist'
    dependsOn buildWeb
//    dependsOn cleanAssets
}

/**
 * Hooks tasks into running as part of the app module build.
 */
tasks.named("preBuild") {
//    dependsOn copyDistToAssets
    dependsOn buildWeb
//    dependsOn cleanAssets
}

/**
 * Includes tasks when running a project clean.
 */
tasks.named('clean') {
    dependsOn cleanWeb
    dependsOn cleanAssets
}